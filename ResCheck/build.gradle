buildscript {
    repositories {
        google()
        jcenter()
    }
}
apply plugin: 'com.android.application'
apply plugin: 'checkstyle'

repositories {
    mavenCentral()
}

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    compileSdkVersion project.ext.versionCompileSdk
    buildToolsVersion project.ext.versionBuildTools
	signingConfigs {
        defaultSigning {
			if (project.hasProperty('storeFilePath')) {
				storeFile file(storeFilePath)
            } else {
				storeFile file('signing/debug.keystore')
			}
			
			if (project.hasProperty('storePasswordV')) {
				storePassword storePasswordV
            } else {
				storePassword 'android'
			}
			
			if (project.hasProperty('keyAliasV')) {
				keyAlias keyAliasV
            } else {
				keyAlias 'androiddebugkey'
			}
			
			if (project.hasProperty('keyPasswordV')) {
				keyPassword keyPasswordV
            } else {
				keyPassword 'android'
			}

			println '\n\n'
			println 'CONF: build storeFile is set to ' + storeFile
			println 'CONF: build storePassword is set to ' + storePassword
			println 'CONF: build keyAlias is set to ' + keyAlias
			println 'CONF: build keyPassword is set to ' + keyPassword
            println 'CONF: build versionName is set to ' + project.ext.versionName
            println 'CONF: build versionCode is set to ' + project.ext.versionCode
        }
    }

	defaultConfig {
        applicationId "com.harrikirik.rescheck"
        minSdkVersion project.ext.versionMinSdkVersion
        targetSdkVersion project.ext.versionTargetSdk
        versionName project.ext.versionName
        versionCode project.ext.versionCode
        signingConfig signingConfigs.defaultSigning
    }
    buildTypes {
        release {
            // Set the next line to TRUE to enable proguard
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }

        debug {
            signingConfig signingConfigs.defaultSigning
        }
    }
    lintOptions {
        checkReleaseBuilds true
        abortOnError true
        // Level depends on the type of the job
        /// Fail the build if buildRelease target is called and we have missing translations!
        project.ext.isRelease ? error("MissingTranslation") : warning("MissingTranslation")
        /// Hardcoded text should not exist or should have case by case exceptions
        error "HardcodedText"
        // Warning level things
        warning "ImpliedQuantity"
        warning "InvalidPackage"
        warning "GradleCompatible"
    }
}

def GROUP_BUILD_TASKS = 'Build'
task buildAllDebug(dependsOn: ['clean', 'build'])
buildAllDebug.group = GROUP_BUILD_TASKS
buildAllDebug.description = "Build and publish the debug version with GreenhouseCI"

task buildAllRelease(dependsOn: ['buildAllDebug'])
buildAllRelease.group = GROUP_BUILD_TASKS
buildAllRelease.description = "Build and publish the release version with GreenhouseCI"

build.mustRunAfter clean

check.dependsOn 'checkstyle'

task checkstyle(type: Checkstyle) {
    configFile file("../config/checkstyle/checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()
}

dependencies {
    // For Google ActionBar
    implementation "androidx.appcompat:appcompat:$rootProject.ext.versionAppCompat"
    implementation "androidx.recyclerview:recyclerview:$rootProject.ext.versionRecyclerView"
    implementation "com.timehop.stickyheadersrecyclerview:library:$rootProject.ext.versionStickyHeadersRecyclerView"
    implementation "androidx.leanback:leanback:$rootProject.ext.versionLeanback"
}